plugins {
    id 'org.springframework.boot' version '2.0.0.RELEASE' apply false
}

repositories {
    jcenter()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.1.0'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
        testCompile 'org.assertj:assertj-core:3.9.1'
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }
}

project(':model') {
    dependencies {
        compile 'org.springframework:spring-context'

        compile 'org.ow2.asm:asm:6.1'

        compile 'com.github.javaparser:javaparser-core:3.5.6'
        compile 'com.github.javaparser:java-symbol-solver-core:0.6.3'
    }
}

project(':diagram-plantuml') {
    dependencies {
        compile 'org.springframework:spring-context'
        compile 'net.sourceforge.plantuml:plantuml:8059'
        compile project(':model')

        testCompile 'org.springframework:spring-test'
    }
}

project(':infrastructure-jdeps') {
    dependencies {
        compile files("${System.getProperty('java.home')}/../lib/tools.jar")
        compile project(':model')
    }
}

project(':infrastructure-mybatis') {
    dependencies {
        compile 'org.mybatis:mybatis:3.4.5'
        compile project(':model')
    }

    test {
        dependsOn ':sut:compileJava', ':sut:processResources'
    }
}

project(':web') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile project(':diagram-plantuml')
        compile project(':infrastructure-jdeps')

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':package-diagram-cli') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':model')
        compile project(':diagram-plantuml')
        compile project(':infrastructure-jdeps')
        compile 'org.springframework.boot:spring-boot-starter'
    }
}

project(':class-list-cli') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':model')
        compile 'org.springframework.boot:spring-boot-starter'
        compile 'org.apache.poi:poi:3.17'
        compile 'org.apache.poi:poi-ooxml:3.17'

        testCompile('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit'
        }
    }

    test {
        dependsOn ':sut:compileJava'
    }
}

project(':sut') {
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1'
        runtime 'com.h2database:h2'
    }

    sourceSets {
        main {
            resources.srcDirs = ["src/main/java", "src/main/resources"]
        }
    }
}

project(':shell') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':model')
        compile project(':diagram-plantuml')
        compile project(':infrastructure-jdeps')
        compile 'org.springframework.boot:spring-boot-starter'
        compile 'org.springframework.shell:spring-shell-starter:2.0.0.RELEASE'

        compile 'com.fasterxml.jackson.core:jackson-core'
        compile('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit'
        }
    }
}
