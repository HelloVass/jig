plugins {
    id 'org.springframework.boot' version '1.5.10.RELEASE'
}

repositories {
    jcenter()
}
subprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.9.0'
    }
}

project(':model') {
}

project(':diagram-plantuml') {
    dependencies {
        compile 'org.springframework:spring-context:4.3.14.RELEASE'
        compile 'net.sourceforge.plantuml:plantuml:8059'
        compile project(':model')
    }
}
project(':infrastructure-jdeps') {
    dependencies {
        compile files("${System.getProperty('java.home')}/../lib/tools.jar")
        compile project(':model')
    }
}
project(':infrastructure-javaparser') {
    dependencies {
        compile 'com.github.javaparser:javaparser-core:3.5.6'
        compile 'com.github.javaparser:java-symbol-solver-core:0.6.3'
        compile project(':model')
    }
}

project(':web') {
    apply plugin: 'org.springframework.boot'

    ext['thymeleaf.version'] = '3.0.9.RELEASE'
    ext['thymeleaf-layout-dialect.version'] = '2.2.2'
    ext['thymeleaf-extras-springsecurity4.version'] = '3.0.2.RELEASE'
    ext['thymeleaf-extras-java8time.version'] = '3.0.1.RELEASE'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile project(':diagram-plantuml')
    }
}

project(':package-diagram-cli') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':model')
        compile project(':diagram-plantuml')
        compile project(':infrastructure-jdeps')
        compile project(':infrastructure-javaparser')
        compile 'org.springframework.boot:spring-boot-starter'
    }
}

project(':class-list-cli') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':model')
        compile project(':infrastructure-javaparser')
        compile 'org.springframework.boot:spring-boot-starter'
        compile 'com.fasterxml.jackson.core:jackson-core'
        compile 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':sut') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web'
        compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.1")
    }

    bootRepackage {
        enabled = false
    }
}


project(':experimental') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter'
        compile project(':model')
    }

    bootRepackage {
        enabled = false
    }
}
